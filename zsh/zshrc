[[ -f "$HOME/.zshrc-pre" ]] && source "$HOME/.zshrc-pre"

[[ -x /opt/homebrew/bin/brew ]] && eval $(/opt/homebrew/bin/brew shellenv)
[[ -x /opt/homebrew/bin/gcc-11 ]] && export CC=/opt/homebrew/bin/gcc-11
[[ -f /opt/dev/sh/chruby/chruby.sh ]] && { type chruby >/dev/null 2>&1 || chruby () { source /opt/dev/sh/chruby/chruby.sh; chruby "$@"; } }

# pass unmatched globs verbatim to the command, allowing things like `HEAD^` to work
setopt NO_NOMATCH

setopt promptsubst
setopt inc_append_history
setopt share_history

HISTSIZE=1000
SAVEHIST=50000
HISTFILE=${XDG_STATE_HOME:-$HOME/.local/state}/.zsh_history

export CLICOLOR=1

path+=("$HOME/dotfiles/bin")

# Keys {{{ 

# Rebind Alt-Dot after prezto breaks it..
bindkey '^[1' digit-argument
bindkey '^[2' digit-argument
bindkey '^[3' digit-argument
bindkey '^[4' digit-argument
bindkey '^[5' digit-argument
bindkey '^[6' digit-argument
bindkey '^[7' digit-argument
bindkey '^[8' digit-argument
bindkey '^[9' digit-argument
bindkey '^[.' insert-last-word
bindkey '^A' beginning-of-line
bindkey '^[OH' beginning-of-line
bindkey '^E' end-of-line
bindkey '^[OF' end-of-line
bindkey '<M-b>' backward-word
bindkey '<M-f>' forward-word

# Alt-Comma to copy earlier arguments after Alt-Dot
autoload -U copy-earlier-word
zle -N copy-earlier-word
bindkey '^[,' copy-earlier-word

# Ctrl-O to stash the current line
bindkey '^o' push-line

# }}}

# Aliases {{{ 

alias st='git status'
alias ci='git commit'
alias co='git checkout'
alias gd='git diff'
alias gdc='git diff --cached'
alias gmt='git mergetool'
alias br='git branch'
alias show='git show'
alias rb='git rebase'
alias rbc='git rebase --continue'
alias rbi='git rebase -i --autosquash'
alias rbim='git rebase -i --autosquash origin/main'
alias fup='git fixup'

alias pff='git pull --ff-only'
alias pnf='git pull --no-ff'

alias mff='git merge --ff-only'
alias mnf='git merge --no-ff'

alias po='git push origin'
alias pof='git push origin --force-with-lease'
alias gph='git push origin --set-upstream HEAD'

alias ga='git add'
alias gap='git add --patch'

alias gfu='git fetch origin'
alias gfo='git fetch origin'

alias pr='gh pr create --web'

alias rg='rg -S'
alias prl='pr -l'

alias stfu="osascript -e 'set volume output muted true'"
alias flushdns="dscacheutil -flushcache"

alias vw="nvim +VimwikiIndex +'lcd %:p:h'"

alias tm='tmux new-session -A'

# }}}

# function H {{{ 

function h() {
  if [[ -z $1 ]]; then
    fc -l
    return
  fi

  fc -l 0 | grep "$@"
}

# }}}

# function setup_macos {{{ 

function setup_macos() {
    # Faster keyboard repeat rate. The defaults available are so slow and it takes ages to delete a lot of text.
    defaults write NSGlobalDomain KeyRepeat -int 1
    defaults write NSGlobalDomain InitialKeyRepeat -int 12
    defaults write com.microsoft.VSCode ApplePressAndHoldEnabled -bool false
    defaults write -g ApplePressAndHoldEnabled -bool false

    # Show hidden files in finder. I want to see everything.
    defaults write com.apple.finder AppleShowAllFiles YES
}
# }}}

# Install & Setup zinit {{{ 

if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# }}}

# Install & configure plugins {{{ 

# Annexes for zinit
zi light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

function zvm_after_lazy_keybindings() {
    zvm_bindkey vicmd 'k' history-substring-search-up
    zvm_bindkey vicmd 'j' history-substring-search-down
    zvm_bindkey vicmd '[A' history-substring-search-up
    zvm_bindkey vicmd '[B' history-substring-search-down
}

zi ice depth=1
zi light jeffreytse/zsh-vi-mode

zi wait lucid for \
  atinit"zicompinit; zicdreplay" \
      zdharma-continuum/fast-syntax-highlighting \
  blockf atpull'zinit creinstall -q .' \
      zsh-users/zsh-completions

zinit ice wait"0a" lucid atload'
    bindkey "$terminfo[kcuu1]" history-substring-search-up;
    bindkey "$terminfo[kcud1]" history-substring-search-down;
    bindkey "^[[A" history-substring-search-up;
    bindkey "^[[B" history-substring-search-down;
    '
zinit load zsh-users/zsh-history-substring-search

zinit ice wait"0b" lucid atload"_zsh_autosuggest_start"
zinit load zsh-users/zsh-autosuggestions

zi ice as"completion"
zi load "${HOME}/dotfiles/zsh/completion"

zi ice light-mode link has'tmux'
zi snippet "$HOME/dotfiles/zsh/snippets/zsh-tmux-auto-title.plugin.zsh"

# Set up starship theme
export STARSHIP_CONFIG="${STARSHIP_CONFIG:-${0:a:h}/starship.toml}"
zi ice as"command" from"gh-r" \
          atclone"./starship init zsh > init.zsh; ./starship completions zsh > _starship" \
          atpull"%atclone" src"init.zsh"
zi light starship/starship

# }}}

[[ -f "$HOME/.zshrc-post" ]] && zi ice light-mode link && zi snippet "$HOME/.zshrc-post"

# vim:foldmethod=marker:foldlevel=0:
